% This file make possible for partners to get status effects and handle a few other things too.

% Make partners able to get any status
% and save the status data in $ExtraData - Debuff Statuses - Sleep, Poison, Frozen, Dizzy, Fear, Paralyze, Shrink, Stop
%
% From ~Func:inflict_status
@Hook 1948A4 % 80265FC4
{
    PUSH    A0, A1, V0
    % V1 = current Actor
    ADDIU	V0, R0, 0100 % .Actor:Partner
    BNE     V0, V1, .CheckPlayer
    NOP
    % Save Partner Extra Data
    % S0 = $actor
    % S1 = statusID
    % S2 = turns
    CLEAR   A0 % save statusID
    JAL     $SetPartnerExtraBattleData
    COPY    A1, S1
    ORI     A0, R0, 1 % save turns to wait
    JAL     $SetPartnerExtraBattleData
    COPY    A1, S2
    % nuke defense byte
    ORI     A0, R0, E % defense byte
    JAL     $SetPartnerExtraBattleData
    CLEAR   A1
    BEQ     R0, R0, .End
    NOP
    .CheckPlayer
    BNE     V1, R0, .End
    NOP
    % If player got debuffed, clear his defense byte
    LIO     V0, $ExtraActorData_Everyone
    SB      R0, E (V0) % clear mario's defense byte
    .End
    POP     A0, A1, V0
    J       80265FCC
    NOP
}

% Make partners able to get any status
% and save the status data in $ExtraData - Static Status
%
% From ~Func:inflict_status
@Hook 194A1C % 8026613C
{
    PUSH    A0, A1, V0
    % V1 = current Actor
    ADDIU	V0, R0, 0100 % .Actor:Partner
    BNE     V0, V1, .End
    NOP
    % Save Partner Extra Data
    % S1 = statusID
    % S2 = turns
    ORI     A0, R0, 2 % save static turns left
    JAL     $SetPartnerExtraBattleData
    COPY    A1, S2
    .End
    POP     A0, A1, V0
    J       80266144
    NOP
}

% Make partners able to get any status
% and save the status data in $ExtraData - Transparent Status
%
% From ~Func:inflict_status
@Hook 194ADC % 802661FC
{
    PUSH    A0, A1, V0
    % V1 = current Actor
    ADDIU	V0, R0, 0100 % .Actor:Partner
    BNE     V0, V1, .End
    NOP
    % Save Partner Extra Data
    % S2 = turns
    ORI     A0, R0, 3 % save turns to wait
    JAL     $SetPartnerExtraBattleData
    COPY    A1, S2
    .End
    POP     A0, A1, V0
    J       80266204
    NOP
}

% Make partners able to get any status
% and save the status data in $ExtraData - Stone Status % unused
%
% From ~Func:inflict_status
@Hook 194A70 % 80266190
{
    PUSH    A0, A1, V0
    % V1 = current Actor
    ADDIU	V0, R0, 0100 % .Actor:Partner
    BNE     V0, V1, .End
    NOP
    % Save Partner Extra Data
    % S1 = statusID
    % S2 = turns
    CLEAR   A0 % save statusID
    JAL     $SetPartnerExtraBattleData
    COPY    A1, S1
    ORI     A0, R0, 1 % save turns to wait
    JAL     $SetPartnerExtraBattleData
    COPY    A1, S2
    .End
    POP     A0, A1, V0
    J       80266198
    NOP
}

% Make partners able to get any status
% and save the status data in $ExtraData - Frozen, create pillar init
%
% From ~Func:inflict_status
@Hook 194910 % 80266030
{
    PUSH    A0, A1, V0
    % V1 = current Actor
    ADDIU	V0, R0, 0100 % .Actor:Partner
    BNE     V0, V1, .End
    NOP
    % Save Partner Extra Data
    % S1 = statusID
    % S2 = turns
    CLEAR   A0 % save statusID
    JAL     $SetPartnerExtraBattleData
    COPY    A1, S1
    ORI     A0, R0, 1 % save turns to wait
    JAL     $SetPartnerExtraBattleData
    COPY    A1, S2
    .End
    POP     A0, A1, V0
    J       80266038
    NOP
}

% Make possible to swap partners even if a
% partner have an status
@Function 191BA8 % 802632C8
{
    J	802632D8
}

% Make possible to swap partners even if a
% partner have an status - Frozen Status
@Function 191BAC % 802632CC
{
    ADDIU	V0, R0, 00FF
}

% Partner - Don't decrease debuff turns when the game tries to do a Partner turn
%
% From ~Func:btl_state_update_begin_partner_turn
@Hook 1715A8 % 80242CC8
{
    % Skip partners turn when they has a debuff
    %
    % check if debuff is Poisoned or Shrink
    LBU     A1, 210 (S0) % debuff
    ORI     A2, R0, 9 % .Status:Poison
    BEQ     A1, A2, .End
    ORI     A2, R0, A % .Status:Shrink
    BEQ     A1, A2, .End
    NOP
    ORI     A1, R0, 1 % skip turn
    SAW     A1, 8029F254
    .End
    % Skip the decreasing turns code, is now placed at the start of Mario's turn
    J	    80242D20
    NOP
}

% For partners don't show the debuff counter if current status is shrink
%
% ~Func:btl_update_ko_status
@Hook:Battle 802677F0
{
    LBU	    V1, 0210 (S0) % debuff
    ORI     A2, R0, A % .Status:Shrink
    BEQ     V1, A2, .SkipKO
    NOP
    % restore vanilla code
    LBU	    V1, 0211 (S0)
    J       802677F8
    SB	    V0, 0216 (S0)
    .SkipKO
    J       802677FC
    NOP
}

% Partner - Decrease turns left when the player's team turn starts
% instead when the game tries to do Partner turn - Debuff Statuses
%
% Player - update player's buffs at the start of the player next turn
%
% Enemies - update enemies's buffs at the start of the player next turn
%
% Also heal all actors with HP & FP buffs
%
% From ~Func:btl_state_update_begin_player_turn
@Hook 170F00 % 80242620
{
    JAL     $Function_HPFBBuffs_Heal_Init % from Battle_NewStatuses.patch
    NOP
    %JAL     $Function_DecreasePartnerStatusTurns
    NOP
    JAL     $Function_AdvancePartnersStatus
    NOP
    JAL     $Function_AdvanceBuffData_Actor
    CLEAR   A0 % .Actor:Player
    % update enemies extraData and vanilla's debuffs
    JAL     $Function_AdvanceEnemiesStatus
    NOP
    .End
    % restore vanilla code
    ADDIU	A0, R0, 000E
    J       80242628
    LW	    V0, 0000 (S1)
}

% Update enemies debuffs plus new statuses buffs and debuffs.
%
% Code grabbed from func_80242FE0 (1718C0)
% I just grabbed the part that updates all debuffs.
#new:Function:Battle $Function_AdvanceEnemiesStatus
{
      PUSH      RA, A0, A1, A2, A3, V0, V1, S0, S1, S2, S3, S4, S5, S6, F20, F22, F24
      LIO       S6, 800DC070
      LW	    S5, 00D8 (S6) % playerActor*
      LW	    S2, 00DC (S6) % partnerActor*
110:  CLEAR     S4
114:  COPY      S1, S4
118:  COPY      S3, S6
      .o11C
11C:  LW        S0, E0 (S3)
120:  BEQL      S0, R0, .o150
124:  ADDIU     S1, S1, 1
128:  LW        V0, 1D8 (S0)
12C:  BEQL      V0, R0, .o150
130:  ADDIU     S1, S1, 1
134:  LW        A0, 1E8 (S0)
138:  JAL       ~Func:does_script_exist
13C:  NOP
140:  BEQL      V0, R0, .o14C
144:  SW        R0, 1D8 (S0)
148:  ADDIU     S4, R0, 1
      .o14C
14C:  ADDIU     S1, S1, 1
      .o150
150:  SLTI      V0, S1, 18
154:  BNE       V0, R0, .o11C
158:  ADDIU     S3, S3, 4
15C:  BNE       S4, R0, .End
160:  NOP
164:  JAL       ~Func:reset_actor_turn_info
168:  CLEAR     S1
16C:  ADDIU     S3, R0, 14
170:  LIA       S4, 800DC070
178:  SLL       V0, S1, 2
      .o17C
17C:  ADDU      V0, S6, V0
180:  LW        S0, E0 (V0)
184:  BEQL      S0, R0, .o32C
188:  ADDIU     S1, S1, 1
18C:  LIO       A0, FFFEFFFF
194:  LW        V0, 0 (S0)
198:  LUI       V1, C00
19C:  OR        V0, V0, V1
1A0:  AND       A0, V0, A0
1A4:  LB        V0, 210 (S0)
1A8:  LBU       V1, 210 (S0)
1AC:  BEQ       V0, R0, .o204
1B0:  SW        A0, 0 (S0)
1B4:  ADDIU     V0, V1, FFFD
1B8:  SLTIU     V0, V0, 6
1BC:  BEQ       V0, R0, .o1CC
1C0:  LUI       V0, 1
1C4:  OR        V0, A0, V0
%1C8:  SW        V0, 0 (S0) - set actor flag 00010000 as true (it skips this actor turn), it's no longer neccesary, because now this function is placed at the start of the player turn instead of the enemies' turns.
      .o1CC
1CC:  LBU       V0, 211 (S0)
1D0:  ADDIU     V0, V0, FFFF
1D4:  SB        V0, 211 (S0)
1D8:  SLL       V0, V0, 18
1DC:  BGTZ      V0, .o204
1E0:  NOP
1E4:  LH        A0, 436 (S0)
1E8:  JAL       ~Func:remove_status_1
1EC:  SB        R0, 210 (S0)
1F0:  COPY      A0, S0
1F4:  JAL       ~Func:dispatch_event_actor
1F8:  ADDIU     A1, R0, 31 % .Status:RecoverStatus
1FC:  SAW       S3, 8029F258
      .o204
204:  LB        V0, 212 (S0)
208:  BEQ       V0, R0, .o23C
20C:  NOP
210:  LBU       V0, 213 (S0)
214:  ADDIU     V0, V0, FFFF
218:  SB        V0, 213 (S0)
21C:  SLL       V0, V0, 18
220:  BGTZ      V0, .o23C
224:  NOP
228:  LH        A0, 436 (S0)
22C:  JAL       ~Func:remove_status_2
230:  SB        R0, 212 (S0)
234:  SAW       S3, 8029F258
      .o23C
23C:  LB        V0, 218 (S0)
240:  BEQ       V0, R0, .o274
244:  NOP
248:  LBU       V0, 219 (S0)
24C:  ADDIU     V0, V0, FFFF
250:  SB        V0, 219 (S0)
254:  SLL       V0, V0, 18
258:  BGTZ      V0, .o274
25C:  NOP
260:  LH        A0, 436 (S0)
264:  JAL       ~Func:remove_status_3
268:  SB        R0, 218 (S0)
26C:  SAW       S3, 8029F258
      .o274
274:  LB        V0, 214 (S0)
278:  BEQ       V0, R0, .o2A4
27C:  NOP
280:  LBU       V0, 215 (S0)
284:  ADDIU     V0, V0, FFFF
288:  SB        V0, 215 (S0)
28C:  SLL       V0, V0, 18
290:  BGTZ      V0, .o2A4
294:  NOP
298:  SB        R0, 214 (S0)
29C:  SAW       S3, 8029F258
      .o2A4
2A4:  LBU       V0, 211 (S0)
2A8:  LB        V1, 217 (S0)
2AC:  SB        V0, 217 (S0)
2B0:  SLL       V0, V0, 18
2B4:  SRA       V0, V0, 18
2B8:  BLEZ      V0, .o2DC
2BC:  NOP
2C0:  LW        V1, 440 (S0)
2C4:  ADDIU     V0, R0, D
2C8:  SB        V0, 216 (S0)
2CC:  LB        V0, 217 (S0)
2D0:  LW        V1, C (V1)
2D4:  BEQ       R0, R0, .o2F4
2D8:  SW        V0, 3C (V1)
      .o2DC
2DC:  BEQ       V1, V0, .o2F4
2E0:  NOP
2E4:  LW        V0, 440 (S0)
2E8:  SB        R0, 216 (S0)
2EC:  LW        V0, C (V0)
2F0:  SW        R0, 3C (V0)
      .o2F4
      JAL       $Function_AdvanceBuffData_Actor
      LH        A0, 192 (S0) % $actor(0x192)=actorID
2F4:  LB        V1, 210 (S0)
2F8:  ADDIU     V0, R0, 9
2FC:  BNEL      V1, V0, .o32C
300:  ADDIU     S1, S1, 1
      % poison - damage enemy
304:  COPY      A0, S0
308:  ADDIU     A1, R0, 1
30C:  LW        V0, 0 (S4)
310:  ADDIU     A2, R0, A
314:  ORI       V0, V0, 20
318:  JAL       ~Func:dispatch_damage_event_actor_0
31C:  SW        V0, 0 (S4)
      % check if actor died
      LBU       A0, 1B8 (S0) % actorHP
      ORI       V0, R0, 14
      BEQL      A0, R0, .UpdateWaitTimer
      ORI       V0, R0, 64
      .UpdateWaitTimer
      % don't overwrite if timer is already 0x64
      ORI       A0, R0, 64
      LAW       V1, 8029F258
      BEQ       A0, V1, .SkipTimer
      NOP
320:  SAW       V0, 8029F258 % battle wait timer
      .SkipTimer
328:  ADDIU     S1, S1, 1
      .o32C
32C:  SLTI      V0, S1, 18
330:  BNE       V0, R0, .o17C
334:  SLL       V0, S1, 2
      .End
      POP       RA, A0, A1, A2, A3, V0, V1, S0, S1, S2, S3, S4, S5, S6, F20, F22, F24
      JR        RA
      NOP
}

% I grabbed above code from Func_80242FE0, now that code
% is already here, skip all that from the original function
%
% func_80242FE0
@Function:Battle 1719D0 % 802430F0
{
    J   8024331C
    NOP
}

% unused, i don't even remember wtf is this XD
#new:Function:Battle $Function_DecreasePartnerStatusTurns
{
    PUSH    RA, A0, A1, A2, A3, V0, V1, S0, S1, S3
    % Check if Partner is available
    JAL     $GetPartnerHP
    NOP
    BLEZ    V0, .End % Partner is dead or partner don't have a partner compatible with HP
    NOP
    % A1 = 8029F254 (?) % Is camera/screen related data
    % S0 = $actor
    % S1 = battleStatus (800DC070)
    % S3 = 8029F258 (?)
    %=================
    % Debuff Status
    %=================
    LBU     V0, 0217 (S0)
    ADDIU   V0, V0, FFFF
    SB      V0, 0217 (S0)
    ADDIU   V0, R0, 0001
    SW      V0, 0000 (A1)
    ADDIU   V0, R0, 0014
    SW      V0, 0000 (S3)
    LB      V1, 0217 (S0)
    BEQ     V1, R0, .RecoverFromStatus
    ADDIU   A0, R0, 0034 % .Event:RecoverStatusPartner
    LW      V0, 0440 (S0)
    LW      V0, 000C (V0)
    BEQ     R0, R0, .End
    SW      V1, 003C (V0)
    .RecoverFromStatus
    JAL     ~Func:dispatch_event_partner
    SB      R0, 0216 (S0)
    LW      V0, 0440 (S0)
    LW      V0, 000C (V0)
    SW      R0, 003C (V0)
    LW      V0, 0004 (S1)
    ORI     V0, V0, 0008
    SW      V0, 0004 (S1)
    .End
    POP     RA, A0, A1, A2, A3, V0, V1, S0, S1, S3
    JR      RA
    NOP
}

% Advance current partner status data
#new:Function:Battle $Function_AdvancePartnersStatus
{
    PUSH    RA, A0, A1, A2, A3, V0, V1, T0, S0, S1
    JAL     $AdvancePartnersExtraData
    NOP
    %===== Check if Partner is Dead======
    JAL     $GetPartnerHP
    NOP
    BLEZ     V0, .End
    NOP
    % get partner's actor data
    JAL     ~Func:get_actor
    ORI     A0, R0, 100 % .Actor:Partner
    JAL     $GetPartnerExtraBattleData_Pointer
    COPY    A1, V0 % copy $actor in A1
    COPY    A0, V0 % copy $extraData in A0
    COPY    S0, A0
    COPY    S1, A1
    LBU     V0, 0 (A0) % load status
    % V0 = partner's status
    BEQ     V0, R0, .Static
    NOP
    % Partner has a status
    LBU     V0, 1 (A0) % load status' turns left
    BNE     V0, R0, .DecreaseDebuffTurn
    NOP
    % Recover from Status
    %
    % clear status
    .ClearStatus
    %====================
    % Clear debuff status
    %====================
    /%
    check if status is Frozen

    The game does this automatically, skip this

    ORI     V1, R0, 7 % .Status:Frozen
    BNE     V0, V1, .ClearStatus_SkipPillar
    NOP
    LW	    V1, 0228 (A1) % load pillar data
    LW	    V0, 0000 (V1) % load fx flags
    ORI	    V0, V0, 0010 % destroy pillar
    SW	    V0, 0000 (V1) % set flags
    .ClearStatus_SkipPillar
    %/
    % clear status
    SB      R0, 210 (A1) % status
    SB      R0, 211 (A1) % status turns
    SB      R0, 0 (A0) % clear status (ExtraData)
    SB      R0, 1 (A0) % clear status turns (ExtraData)
    % clear icon
    JAL     80047898 % ~Func:remove_status_icon_debuff
    LHU     A0, 436 (A1) % load iconDataIndex
    % clear KO status
    LB      T0, 216 (A1) % load KO status
    BEQ     T0, R0, .Static
    NOP
    SB      R0, 216 (A1) % clear KO status
    SB      R0, 217 (A1) % clear KO turns
    % Clear X icon
    ORI     T0, R0, 1
    SAW     T0, 8029F254 % ?
    ORI     T0, R0, 20`
    SAW     T0, 8029F258 % frames to wait
    LW      T0, 440 (A1) % load debuffIconData
    LW      T0, C (T0) % load X icon data
    SW      R0, 3C (T0) % set X's icon turns left
    JAL     ~Func:dispatch_event_partner
    ADDIU   A0, R0, 0034 % .Event:RecoverStatusPartner
    % pop A registers
    COPY    A0, S0
    COPY    A1, S1
    BEQ     R0, R0, .Static
    NOP
    %====================
    .DecreaseDebuffTurn
    %====================
    LB      V0, 211 (A1) % load status turns
    ADDI    V0, V0, FFFF
    SB      V0, 211 (A1) % save status turns
    % wait a few frames (need for the X icon and the recover animations)
    ORI     T0, R0, 1
    SAW     T0, 8029F254 % ?
    ORI     T0, R0, 20`
    SAW     T0, 8029F258 % frames to wait
    LW      T0, 440 (A1) % load debuffIconData
    LW      T0, C (T0) % load X icon data
    SW      V0, 3C (T0) % set X's icon turns left
    % check if partner is knock out
    SB      V0, 217 (A1) % save KO turns
    LBU     V0, 1 (A0) % load status' turns left
    BEQ     V0, R0, .ClearStatus
    NOP
    .Static
    % Check if partner has static
    LBU     V0, 2 (A0)
    BEQ     V0, R0, .Invisible
    NOP
    % It has static
    ADDI    V0, V0, FFFF
    SB      V0, 2 (A0) % save static turns (from $ExtraData)
    SB      V0, 213 (A1) % save static turns (from $Actor)
    BNE     V0, R0, .Invisible
    NOP
    % Static is over, nuke static status
    SB      R0, 2 (A0) % static turns (from $ExtraData)
    SB      R0, 212 (A1) % static state
    SB      R0, 213 (A1) % static turns (from $Actor)
    % clear icon
    JAL     800479A0 % ~Func:remove_status_static
    LHU     A0, 436 (A1) % load iconDataIndex
    .Invisible
    % Check if partner is invisible
    %
    % pop A registers
    COPY    A0, S0
    COPY    A1, S1
    LBU     V0, 3 (A0)
    BEQ     V0, R0, .End
    NOP
    % It is invisible
    ADDI    V0, V0, FFFF
    SB      V0, 3 (A0) % save static turns (from $ExtraData)
    SB      V0, 219 (A1) % save static turns (from $Actor)
    BNE     V0, R0, .End
    NOP
    % transparency is over, nuke status
    SB      R0, 3 (A0) % invisible turns (from $ExtraData)
    SB      R0, 218 (A1) % invisible state
    SB      R0, 219 (A1) % invisible turns (from $Actor)
    % clear icon
    JAL     80047AA8 % ~Func:remove_status_invisible
    LHU     A0, 436 (A1) % load iconDataIndex
    .End
    JAL     ~Func:btl_update_ko_status % update icons
    NOP
    POP     RA, A0, A1, A2, A3, V0, V1, T0, S0, S1
    JR      RA
    NOP
}

% Battle - Swapping partners - Former partner is about to leave
% Check if this partner had an status to disable this partner
% turn, also free all status icons.
%
% From ~Func:btl_state_update_change_partner
@Hook:Battle 17402C % 8024574C
{
    PUSH    RA, A0, A1, A2, A3, V0, V1
    % Check if partner has static
    JAL     $GetPartnerExtraBattleData
    ORI     A0, R0, 2 % get static turns left
    BEQ     V0, R0, .CheckTransparency
    NOP
    % Partner has static
    SH      R0, 212 (S1) % static status data
    JAL     800479A0 % ~Func:remove_status_icon_static
    LHU     A0, 436 (S1) % load iconDataIndex
    % get icon's "disappear animation" timer
    JAL     $GetStatusIconsData
    LHU     A0, 436 (S1) % load iconDataIndex
    % set timer as zero, to make the icon to be free this same frame
    % instead of waiting until the animation ends.
    SB	    R0, 004D (V0) % static icon animation timer
    .CheckTransparency
    % Check if partner is invisible
    JAL     $GetPartnerExtraBattleData
    ORI     A0, R0, 3 % get static turns left
    BEQ     V0, R0, .CheckDebuffs
    NOP
    % Partner is transparent
    SH      R0, 218 (S1) % transparent status data
    JAL     80047AA8 % ~Func:remove_status_icon_transparent
    LHU     A0, 436 (S1) % load iconDataIndex
    % get icon's "disappear animation" timer
    JAL     $GetStatusIconsData
    LHU     A0, 436 (S1) % load iconDataIndex
    SB	    R0, 005D (V0) % transparent icon animation timer
    .CheckDebuffs
    JAL     $GetPartnerExtraBattleData
    CLEAR   A0
    BEQ     V0, R0, .End
    NOP
    % check if status is poison
    ORI     A0, R0, 9 % .Status:Poison
    BEQ     V0, A0, .FreeDebuffIcon
    NOP
    % check if status is Shrink
    ORI     A0, R0, A % .Status:Shrink
    BEQ     V0, A0, .FreeDebuffIcon
    NOP
    % check if status is Frozen
    ORI     A0, R0, 7 % .Status:Frozen
    BNE     V0, A0 .SkipPartnerTurn
    NOP
    % Remove ice pillar
    JAL 	~Func:sfx_play_sound
    ADDIU	A0, R0, 00E7
    % S1 = partnerActorData
    LW	    V1, 0228 (S1) % load debuffFX data (only used by Frozen?)
    LW	    V0, 0000 (V1)
    ORI	    V0, V0, 0010 % destroy pillar
    SW	    V0, 0000 (V1)
    .SkipPartnerTurn
    % This partner has an status set partner's turn as used
    LIO     A0, 800DC074 % BattleFlags2
    LBU     V0, 3 (A0)
    ORI     V0, V0, 4 % bit2=Partner turn
    SB      V0, 3 (A0)
    .FreeDebuffIcon
    SH      R0, 210 (S1) % debuff data
    JAL     80047898 % ~Func:remove_status_icon_debuff
    LHU     A0, 436 (S1) % load iconDataIndex
    % get icon's "disappear animation" timer
    JAL     $GetStatusIconsData
    LHU     A0, 436 (S1) % load iconDataIndex
    SB	    R0, 003D (V0) % debuff icon animation timer
    .End
    % check if this partner was dead to enable partner's turn once again
    JAL     $GetPartnerHP
    NOP
    BGTZ    V0, .Pop
    NOP
    LIO     A0, 800DC074 % BattleFlags2
    LBU     V0, 3 (A0)
    LIO     V1, FFFFFFFB % disable bit2=Partner turn
    AND     V0, V0, V1
    SB      V0, 3 (A0)
    .Pop
    JAL     $Function_GlitzPit_DontSwapChallenge
    NOP
    POP     RA, A0, A1, A2, A3, V0, V1
    % restore vanilla code
    LUI 	V1, F7FF
    J       80245754
    LW	    V0, 00000 (S0)
}

% V0 = actor's status icon data : A0 = iconDataIndex
#export:Function $GetStatusIconsData
{
    PUSH    A0, V1
    SLL	    V0, A0, 1
    ADDU	V0, V0, A0
    SLL	    V0, V0, 2
    SUBU	V0, V0, A0
    LAW     V1, 800A0F44
    SLL	    V0, V0, 4
    ADDU	V0, V1, V0
    POP     A0, V1
    JR      RA
    NOP
}

% Each status icon has an animation timer, this timer
% is set in the "remove_status_icon" functions and in
% this same funtion once again too.
%
% This hook disables writing in the timer once again,
% this is needed for the above hook, so the status
% icon is freed immediately instead of waiting until
% the animation is done.
%
% Don't overwrite icon's end animation timer - Debuff Statuses
%
% From func_80046030 % update_status_icons(?)
@Function 21680 % 80046280
{
    NOP
}

% Don't overwrite icon's end animation timer - Static Status
%
% From func_80046030 % update_status_icons(?)
@Function 21764 % 80046364
{
    NOP
}

% Don't overwrite icon's end animation timer - Transparent Status
%
% From func_80046030 % update_status_icons(?)
@Function 21848 % 80046448
{
    NOP
}

% Battle - Swapping partners - New partner spawns
% Init all extra data and check if this partner shouldn't
% be available due to a status.
%
% From ~Func:btl_state_update_change_partner
@Hook:Battle 174274 % 80245994
{
    PUSH    R0, R0, A0, A1, A2, V0, V1, S0
    JAL     $GetPartnerExtraBattleData
    ORI     A0, R0, 0 % load status
    BEQ     V0, R0, .LoadBoosts_Defense
    NOP
    % This partner has an status
    %
    % check if status is frozen to init the ice pillar
    ORI     A0, R0, 7 % .Status:Frozen
    BNE     V0, A0, .SkipPillar
    NOP
    % check if pillar has been already created
    LAW     S0, 800DC14C % load $partnerActor
    LW	    V0, 0228 (S0)
    BNE     V0, R0, .SkipPillar
    NOP
    LIF     F0, 1.0 % snow sparkles size
    SWC1	F0, 0010 (SP)
    SW	    R0, 0014 (SP)
    LW	    A1, 0144 (S0) % load target X pos
    LW	    A2, 0148 (S0) % load target Y pos
    LW	    A3, 014C (S0) % load target Z pos
    JAL	    ~Func:playFX_81
    CLEAR   A0
    LH	    A0, 0436 (S0) % ?
    ADDIU	A1, R0, 0007
    JAL	    80047820
    SW	    V0, 0228 (S0) % save pillar data
    % set status in partner data
    .SkipPillar
    LAW     A1, 800DC14C % load $partnerActor
    JAL     $GetPartnerExtraBattleData
    ORI     A0, R0, 0 % load status
    SB      V0, 210 (A1) % set status
    JAL     $GetPartnerExtraBattleData
    ORI     A0, R0, 1 % load status turns
    SB      V0, 211 (A1) % set status turns
    JAL     ~Func:btl_update_ko_status % update icons
    NOP
    % check if is time for the partner to
    % wake up or not
    JAL     $GetPartnerExtraBattleData
    ORI     A0, R0, 1 % load status turns
    BNE     V0, R0, .MakePartnerUnavailable
    NOP
    % is time for partner to wake up from
    % that status
    %
    % nuke status
    %
    % check if status is frozen to remove the pillar
    JAL     $GetPartnerExtraBattleData
    ORI     A0, R0, 0 % statusID
    ORI     A1, R0, 7 % .StatusFrozen
    BNE     V0, A1, .SkipPillar2
    NOP
    LW	    V1, 0228 (S0) % load pillar data
    LW	    V0, 0000 (V1) % load fx flags
    ORI	    V0, V0, 0010 % destroy pillar
    SW	    V0, 0000 (V1) % set flags
    .SkipPillar2
    CLEAR   A1
    JAL     $SetPartnerExtraBattleData
    ORI     A0, R0, 0 % statusID
    JAL     ~Func:btl_update_ko_status % update icons
    NOP
    JAL     ~Func:sfx_play_sound
    ORI     A0, R0, 0352 % jump / status ended
    % set partner turn as used
    .MakePartnerUnavailable
    % check if current status is Poison
    JAL     $GetPartnerExtraBattleData
    ORI     A0, R0, 0 % load status
    ORI     A1, R0, 9 % .Status:Poison
    BEQ     V0, A1 .LoadBoosts_Defense % skip skipping partner turn
    NOP
    % check if current status is Shrink
    ORI     A1, R0, A % .Status:Shrink
    BEQ     V0, A1, .LoadBoosts_Defense
    NOP
    LIO     A0, 800DC074 % BattleFlags2
    LBU     A1, 3 (A0)
    ORI     A1, A1, 14 % bit2=Partner turn, bit4=don't use idle animations
    SB      A1, 3 (A0)
    .LoadBoosts_Defense
    JAL     $GetPartnerExtraBattleData_Pointer
    ORI     A0, R0, 0100 % .Actor:Partner
    COPY    A0, V0
    .GetStatic
    LBU     V0, 2 (A0) % load static status
    BEQ     V0, R0, .GetTransparency
    NOP
    % Player had static
    LAW     A1, 800DC14C % load $partnerActor
    ORI     V1, R0, B % .Status:Static
    SB      V1, 212 (A1) % set staticStatus as True (0B=True)
    SB      V0, 213 (A1) % set static turns left
    JAL     ~Func:btl_update_ko_status % update status icons
    NOP
    .GetTransparency
    JAL     $GetPartnerExtraBattleData_Pointer
    NOP
    COPY    A0, V0
    LBU     V0, 3 (A0) % load invisible status
    BEQ     V0, R0, .End
    NOP
    % Player is invisible
    LAW     A1, 800DC14C % load $partnerActor
    ORI     V1, R0, E % .Status:Transparent
    SB      V1, 218 (A1) % set invisibleStatus as True (0E=True)
    SB      V0, 219 (A1) % set invisible turns left
    JAL     ~Func:btl_update_ko_status % update status icons
    NOP
    .End
    POP     R0, R0, A0, A1, A2, V0, V1, S0
    % restore vanilla code
    JAL     ~Func:does_script_exist
    LW	    A0, 00C0 (S2) % load controlScriptID
    J       802459A0
    NOP
}

% Battle - Swapping partners - End of the function
% Don't disable BattleFlags2(0x04) bit4 if partner has an status (is the one that ignores the idle animations)
%
% Also make possible to don't swap positions between the Player and Partner after the turn ends.
%
% From ~Func:btl_state_update_change_partner
@Hook:Battle 174340 % 80245A60
{
    % Check if this partner have an status to don't disable the bitflag
    COPY    V1, V0 % push V0
    %
    PUSH    A0, A1
    % get partner status
    JAL     $GetPartnerExtraBattleData
    ORI     A0, R0, 0 % statusID
    BEQ     V0, R0, .POP
    NOP
    % check if status is poison
    ORI     A0, R0, 9 % .Status:Poison
    BEQ     V0, A0, .POP
    NOP
    % check if status is Shrink
    ORI     A0, R0, A % .Status:Shrink
    BEQ     V0, A0, .POP
    NOP
    % make Mario and Partner stay in the same position after the turn ends.
    % If actor has a debuff.
    %
    % Check if current turn is Partner
    LW      A0, 0 (S2) % S2=battleStatus
    LUI     A1, 0010 % 00100000 = current turn (00=Player / 01=Partner)
    AND     A1, A0, A1
    BEQ     A1, R0, .POP
    NOP
    % set current turn as player
    LIO     A1, FFEFFFFF % disable 00100000
    AND     A0, A0, A1
    SW      A0, 0 (S2) % battleFlags
    .POP
    POP     A0, A1
    %
    BNE     V0, R0, .DontDisableFlagBit
    COPY    V0, V1 % pop V0
    SW	    V0, 0004 (A0) % disable flag
    .DontDisableFlagBit
    JAL     $Function_UnhideAllEnemiesDebuffIcons
    NOP
    J       80245A68
    LW	    V0, 0000 (A0)
}

% When you swap partners all enemies debuff icons dissapear until the next turn.
%
% Make all icons to reappear once again after the new partner spawns. (Placed in above hook)
#new:Function $Function_UnhideAllEnemiesDebuffIcons
{
    PUSH    RA, A0, A1, A2, A3, V0, V1
    LIO     A0, 800DC148 % battleActors - data table
    ADDIU   A0, A0, 8 % skip Player & Partner data
    CLEAR   A3 % counter
    .Loop
    LW      A1, 0 (A0)
    % All actors has been checked?
    SLTIU   V0, A3, 24` % because there max amount of enemies is 23`
    BEQ     V0, R0, .End
    ADDIU   A0, A0, 4
    BEQ     A1, R0, .Loop % empty data?
    ADDIU   A3, A3, 1
    % Unhidde icons
    LW      A2, 0 (A1) % actorFlags
    % enable 08000000
    LUI     V0, 0800
    OR      A2, A2, V0
    BEQ     R0, R0, .Loop
    SW      A2, 0 (A1) % actorFlags
    .End
    POP     RA, A0, A1, A2, A3, V0, V1
    JR      RA
    NOP
}

% Player - Recover from debuff statuses, don't set turn as used (like in TTYD)
%
% From ~Func:btl_state_update_begin_player_turn
@Hook:Battle 171364 % 80242A84
{
    PUSH    RA, A0, A1, A2, A3, V1
    % Check if Mario just got recover from a status
    LIO     T0, 800DC148
    LW      T0, 0 (T0) % load player battle data
    LBU     T0, 210 (T0) % load debuffID
    BNE     T0, R0, .SkipTurn
    NOP
    % Check Mario pos
    LABU    A0, $MarioPos
    BEQ     A0, R0, .End % Mario is on the left
    NOP
    JAL     ~Func:btl_set_state
    ORI     A0, R0, 7 % .BattleState:BeginPlayerTurn
    BEQ     R0, R0, .End
    NOP
    .SkipTurn
    ORI	    V0, V0, 0002 % skip player turn
    SW	    V0, 0004 (V1)
    .End
    POP     RA, A0, A1, A2, A3, V1
    J       80242A8C
    NOP
}

% Always add 1 more turn for Mario & Partner, needed because above hook
%
% From ~Func:inflict_status
@Hook 194834 % 80265F54
{
    SW	    S2, 0020 (SP)
    % Check if the target is from player's team
    LHU     S2, 192 (A0) % load targetActorID
    SLTIU   S2, S2, 200
    BNEL    S2, R0, .TargetIsFromPlayerTeam
    ADDI    A2, A2, 1
    .TargetIsFromPlayerTeam
    J       80265F5C
    COPY	S2, A2
}

% Zoom the camera and show partner just got a status from an enemy too,
% instead of only Mario.
%
% From ~Func:btl_state_update_enemy_move
@Hook 17619C % 802478BC
{
    % S5 = $playerActor
    % S4 = $partnerActor
    %
    % check Mario
    LB	    V0, 021C (S5) % load statusInflictedID
    BNE 	V0, R0, .GetStatus
    NOP
    % check if player even has a partner
    BEQ     S4, R0, .SkipStatusPause
    NOP
    % check partner too
    LB	    V0, 021C (S4) % load statusInflictedID
    BEQ 	V0, R0, .SkipStatusPause
    NOP
    .GetStatus
    % S5 = $playerActor
    % S4 = $partnerActor
    LUI 	V1, 0C00 % make debuff icons visible
    LW	    V0, 0000 (S5) % load playerActorFlags
    OR	    V0, V0, V1
    BEQ 	S4, R0, .Partnerless
    SW	    V0, 0000 (S5) % set playerActorFlags
    LW	    V0, 0000 (S4) % load partnerActorFlags
    OR	    V0, V0, V1
    SW	    V0, 0000 (S4) % set partnerActorFlags
    .Partnerless
    JAL	    8024E40C % ~Func:btl_cam_use_preset
    ADDIU	A0, R0, 001A % was 0021
    % get current target
    %
    % S6 = battleStatus
    LHU     V0, 1A0 (S6) % load targetID
    BEQ     V0, R0, .End % current target is player
    LBU	    V0, 021C (S5) % load statusInflictedID_player
    LBU	    V0, 021C (S4) % load statusInflictedID_partner
    .End
    % check if this target at least got inflicted by a status (maybe was a multi-target attack and inflicted the first target not the last one)
    BNE     V0, R0, .End_Jump
    NOP
    % This target didn't got status
    LHU     V0, 1A0 (S6) % load targetID
    BEQL    V0, R0, .End_Jump
    LBU	    V0, 021C (S4) % load statusInflictedID_partner % load partner instead
    LBU	    V0, 021C (S5) % load statusInflictedID_player % load player instead
    .End_Jump
    J       802478F0
    NOP
    .SkipStatusPause
    % restore vanilla code
    J       802479C0
    LUI 	V1, 0C00
}

% Make possible for enemies to dispatch the "Actor can't move" message (Stop Watch, etc.)
%
% ~Func:btl_state_update_enemy_move
@Hook:Battle 802478F0
{
    % V0=status about to be inflincted
    ORI     V1, R0, 8 % .Status:Stop
    BEQL    V0, V1, .End
    ORI     V0, R0, 5
    .End
    % restore code
    ADDIU   V0, V0, FFFC
    J       802478F8
    SLL	    V0, V0, 24`
}

% Show how the debuff counter decrease at the start of the next turn
% for Partner too not only Mario.
%
% From ~Func:btl_state_update_end_turn
@Hook 1726AC % 80243DCC
{
    PUSH    T0, T1
    % S5 = $playerActor
    % S3 = $partnerActor
    LUI	    V1, 0C00 % show status icons
    SB	    R0, 021D (S5)
    OR	    V0, V0, V1
    BEQ 	S3, R0, .SkipPartner
    SW	    V0, 0000 (S5) % set playerActorFlags
    % check if current status is Poison, don't do it with this one
    LBU	    T0, 0210 (S3) % load debuff status
    ORI     T1, R0, 9 % .Status:Poison
    BEQ     T0, T1, .SkipPartner
    NOP
    LW	    V0, 0000 (S3)
    OR	    V0, V0, V1
    SW	    V0, 0000 (S3) % set partnerActorFlags
    SB	    R0, 021D (S3)
    .SkipPartner
    POP     T0, T1
    JAL	    80260A60
    NOP
    J   80243DF4
    NOP
}

% Enable Poison Status for partners
%
% From ~Func:btl_state_update_begin_player_turn
@Hook:Battle 170E04 % 80242524
{
    PUSH    RA, A0, A1, A2, A3, V0, V1
    JAL     $GetPartnerHP
    NOP
    BLEZ    V0, .End
    NOP
    LAW     V1, 800DC14C % load partner battle data
    LB	    A0, 0210 (V1) % load debuff status
    ORI     A1, R0, 9 % .Status:Poison
    BNE     A0, A1, .End
    NOP
    % Partner is poisoned
    ORI     A0, R0, 1 % deal 1 damage
    JAL     ~Func:dispatch_damage_event_partner_0
    ORI     A1, R0, A % .Event:Hit
    .End
    POP     RA, A0, A1, A2, A3, V0, V1
    % restore vanilla code
    LIO     S0, 800DC070
    J       8024252C
    RESERVED
}

% Patch function to decrease partner HP instead
% of knocking the partner.
%
% From ~Func:dispatch_damage_event_partner
@Hook 1AE390 % 8027FAB0
{
    PUSH    RA, A0, A1, A2, A3, V0
    JAL     $GetPartnerHP
    NOP
    SUB     A0, V0, A1
    BLEZL   A0, .SetHP
    CLEAR   A0 % set HP as zero
    .SetHP
    JAL     $SetPartnerHP
    NOP
    .End
    POP     RA, A0, A1, A2, A3, V0
    J       8027FAD0
    NOP
}

% Fix this function, get SpritePart -1 data instead of 0,
% Partners usually always use -1, so the game always crash
% with this function, was this even used in vanilla???
%
% From ~Func:dispatch_damage_event_partner
@Function 1AE3C4 % 8027FAE4
{
    ADDI    A2, R0, -1 % use actorPart -1 instead of zero, to stop a crash
}

% Clear all statuses when the battle ends
%
% Placed at Battle_PlayerTeam_DontSwapPositions.patch - Hook 191D2C / 8026344C
%
% From ~Func:btl_check_enemies_defeated

% Don't force partner animation to always be the KO one
% if the status is a debuff.
@Function 1854A0 % 80256BC0
{
    J	    80256BD4
}

% Watt, make possible to use different palettes (neccesary when she is poisoned)
%
% ~Func:appendGfx_npc_actor
@Function:Battle 802569B4
{
    ADDIU	V0, R0, 00FF % impossible partnerID, skip Watt's checks
}

% Watt, dispatch `.Event:Shock` when `EnemyTestTarget` is called and the target was Watt
@Hook:Battle 802773F8
{
    % check if current partner is Watt
    LABU    A0, 8010F2A2 % partnerID
    ORI     V0, R0, 6 % .Partner:Watt
    BEQ     A0, V0, .80277410
    NOP
    % check if target actor is electrified
    LB	    A0, 0212 (S0) % get actor(0x212) - staticStatus
    BEQ	    A0, V1, .80277410
    NOP
    .80277404
    J       80277404
    NOP
    .80277410
    J       80277410
    ADDIU	V0, R0, 0007 % .HitResult:HitStatic
}